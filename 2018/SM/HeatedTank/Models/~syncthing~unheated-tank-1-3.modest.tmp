// Version 1:
// modes.exe unheated-tank-1-3.modest --relative-width --width 0.01 -E "TIME_BOUND=100, C_FAIL=0" -E "TIME_BOUND=200, C_FAIL=0" -E "TIME_BOUND=300, C_FAIL=0" -E "TIME_BOUND=400, C_FAIL=0" -E "TIME_BOUND=500, C_FAIL=0" -E "TIME_BOUND=600, C_FAIL=0" -E "TIME_BOUND=700, C_FAIL=0" -E "TIME_BOUND=800, C_FAIL=0" -E "TIME_BOUND=900, C_FAIL=0" -E "TIME_BOUND=1000, C_FAIL=0" -O unheated-tank-1.results.txt -Y
//
// Version 3:
// modes.exe unheated-tank-1-3.modest --relative-width --width 0.01 -E "TIME_BOUND=100, C_FAIL=0.1" -E "TIME_BOUND=200, C_FAIL=0.1" -E "TIME_BOUND=300, C_FAIL=0.1" -E "TIME_BOUND=400, C_FAIL=0.1" -E "TIME_BOUND=500, C_FAIL=0.1" -E "TIME_BOUND=600, C_FAIL=0.1" -E "TIME_BOUND=700, C_FAIL=0.1" -E "TIME_BOUND=800, C_FAIL=0.1" -E "TIME_BOUND=900, C_FAIL=0.1" -E "TIME_BOUND=1000, C_FAIL=0.1" -O unheated-tank-3.results.txt -Y

const real TIME_BOUND;
const real C_FAIL; // probability for controller action to fail

const real HLP = 3; // overflow level
const real HLB = 1; // upper activation level
const real HLA = -1; // lower activation level
const real HLV = -3; // dry-out level

const real P1_FAIL = 1 / 219; // failure rate of pump P1
const real P2_FAIL = 1 / 175; // failure rate of pump P2
const real V_FAIL = 1 / 320; // failure rate of valve V

action init;

bool on_p1 = true, on_p2 = false, on_v = true; // initial configuration: P1 on, P2 off, V on
int(0..2) fail_p1, fail_p2, fail_v; // 0: not failed, 1: stuck open, 2: stuck closed

var level = 0; // liquid level in tank
der(level) = (on_p1 && fail_p1 == 0 || fail_p1 == 1 ? 0.6 : 0) + (on_p2 && fail_p2 == 0 || fail_p2 == 1 ? 0.6 : 0) + (on_v && fail_v == 0 || fail_v == 1 ? -0.6 : 0);

bool overflow, dryout;

property PFail = Pmax(<>[T<=TIME_BOUND] (overflow || dryout));
//property IFail = (fail_p1 == 0 ? 0 : 1) + (fail_p2 == 0 ? 0 : 1) + (fail_v == 0 ? 0 : 1); // importance function for failure

property PDryOut = Pmax(<>[T<=TIME_BOUND] dryout);
//property IDryOut = (fail_p1 == 2 ? 1 : 0) + (fail_p2 == 2 ? 1 : 0) + (fail_v == 1 ? 1 : 0); // importance function for dryout

property POverflow = Pmax(<>[T<=TIME_BOUND] overflow);
//property IOverflow = (fail_p1 == 1 ? 1 : 0) + (fail_p2 == 1 ? 1 : 0) + (fail_v == 2 ? 1 : 0); // importance function for overflow

process Pump1()
{
   clock c; real x;

   invariant(false) init {= x = Exp(P1_FAIL) =};
   when(c >= x) invariant(c <= x) palt { // failure
   :1: {= fail_p1 = 1 =} // stuck open
   :1: {= fail_p1 = 2 =} // stuck closed
   }
}

process Pump2()
{
   clock c; real x;

   invariant(false) init {= x = Exp(P2_FAIL) =};
   when(c >= x) invariant(c <= x) palt { // failure
   :1: {= fail_p2 = 1 =} // stuck open
   :1: {= fail_p2 = 2 =} // stuck closed
   }
}

process Valve()
{
   clock c; real x;

   invariant(false) init {= x = Exp(V_FAIL) =};
   when(c >= x) invariant(c <= x) palt { // failure
   :1: {= fail_v = 1 =} // stuck open
   :1: {= fail_v = 2 =} // stuck closed
   }
}

process Controller()
{
   process Normal()
   {
      alt {
      :: invariant(level <= HLB) when(level >= HLB) palt {
         :1-C_FAIL: {= on_p1 = false, on_p2 = false, on_v = true =}
         :  C_FAIL: {==}
         }; High()
      :: invariant(level >= HLA) when(level <= HLA) palt {
         :1-C_FAIL: {= on_p1 = true, on_p2 = true, on_v = false =}
         :  C_FAIL: {==}
         }; Low()
      }
   }
   
   process High()
   {
      invariant(level >= HLA) when(level <= HLA) palt {
      :1-C_FAIL: {= on_p1 = true, on_p2 = true, on_v = false =}
      :  C_FAIL: {==}
      }; Low()
   }
   
   process Low()
   {
      invariant(level <= HLB) when(level >= HLB) palt {
      :1-C_FAIL: {= on_p1 = false, on_p2 = false, on_v = true =}
      :  C_FAIL: {==}
      }; High()
   }
   
   Normal()
}

process Observer()
{
   par {
   :: invariant(level <= HLP) when(level >= HLP) {= overflow = true =}
   :: invariant(level >= HLV) when(level <= HLV) {= dryout = true =}
   }
}

par {
:: Pump1()
:: Pump2()
:: Valve()
:: Controller()
:: Observer()
}
